
ext {
    releaseVersion = version
    artifactName = rootProject.name
    repoName = 'RESTDocsEXT'
}

bintray {
	user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = repoName
        name = artifactName
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/RESTDocsEXT/restdocsext-jersey.git'
        issueTrackerUrl = 'https://github.com/RESTDocsEXT/restdocsext-jersey/issues'
        
        version {
            name = releaseVersion
            desc = 'An extension of Spring REST Docs that allows for snippet creation using Jersey Client.'
            released = new Date()
            vcsTag = "v$releaseVersion"
            
            gpg {
                sign = true
                passphrase = project.hasProperty('gpgPassphrase') ? project.property('gpgPassphrase') : 'fail'
            }
            
            mavenCentralSync {
                sync = true
                user = project.property('ossrhUser')
                password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : 'fail'
            } 
        }
        
        //dryRun = true
    }
    publications = ['restdocsPub']
}

task javadocJar (type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar (type : Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        restdocsPub(MavenPublication) {
            from components.java
            groupId group
            artifactId artifactName
            version releaseVersion
            
            artifact sourcesJar
            artifact javadocJar
            
            repositories {
                maven {
                    url "file:/${project.buildDir}/artifacts"
                }
            }
  
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'RESTDocsEXT Jersey'
                    packaging 'jar'
                    description 'An extension of Spring REST Docs that allows for snippet creating using Jersey Client.'
                    url 'https://github.com/RESTDocsEXT/restdocsext-jersey.git'

                    organization {
                        name 'RESTDocsEXT'
                        url 'https://github.com/RESTDocsEXT'
                    }

                    scm {
                        url 'scm:git@github.com:RESTDocsEXT/restdocsext-jersey.git'
                        connection 'scm:git@github.com:RESTDocsEXT/restdocsext-jersey.git'
                        developerConnection 'scm:git@github.com:RESTDocsEXT/restdocsext-jersey.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'psamsotha'
                            name 'Paul Samsotha'
                            email 'paulsamsotha.dev@gmail.com'
                        }
                    }

                    issueManagement {
                        system 'GitHub'
                        url 'https://github.com/RESTDocsEXT/restdocsext-jersey/issues'
                    }
                }
            }
        }
    }
}